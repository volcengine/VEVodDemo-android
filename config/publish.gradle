/*
 * Copyright (C) 2022 bytedance
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 * Create Date : 2022/9/13
 */

apply plugin: 'maven-publish'

def GROUP_ID = 'com.volcengine.vod'
def MAVEN_URL = findProperty('mavenRepo') ?: "../build-publish/repo"
def ARTIFACT_ID = project.name
def VERSION = project.ext.versionName

afterEvaluate {
    publishing {
        repositories {
            maven {
                url = MAVEN_URL
            }
        }
        publications {
            release(MavenPublication) {
                from components.release
                artifact androidSourcesJar
                groupId = GROUP_ID
                artifactId = ARTIFACT_ID
                version VERSION
                pom {
                    name = ARTIFACT_ID
                    description = ARTIFACT_ID + " is part of Volcengine VOD PlayerKit SDK"
                    licenses {
                        license {
                            name = 'The Apache Software License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution = 'repo'
                        }
                    }
                    developers {
                        developer {
                            name = 'Volcengine VOD'
                        }
                    }
                    scm {
                        url = 'https://github.com/TTvcloud/vcloud-sdk-java'
                        connection = 'scm:git:https://github.com/TTvcloud/vcloud-sdk-java.git'
                    }
                }
            }
        }
    }
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}